
buildscript {
  ext {
    kotlinVersion="1.6.20"
    junitVersion="5.8.2"
    slf4jVersion="1.7.36"
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}


version = '0.0.1-SNAPSHOT'
ext.appName = 'FledUtils'

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "org.slf4j:slf4j-api:$slf4jVersion"

  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
  withSourcesJar()
  withJavadocJar()
}

compileJava {
  options.incremental = true
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true
  }
  minHeapSize = "512m"
  maxHeapSize = "2048m"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "11"
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
  }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/fledware/FledUtils"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}
