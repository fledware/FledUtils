
buildscript {
  ext {
    kotlinVersion="1.7.10"
    junitVersion="5.9.0"
    slf4jVersion="2.0.0"
    log4jVersion="2.18.0"
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

version = new File('version.txt').text
group = "io.fledware"
def isSnapshot = version.endsWith("SNAPSHOT")

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  implementation "org.slf4j:slf4j-api:$slf4jVersion"

  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
  testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
  testRuntimeOnly "org.apache.logging.log4j:log4j-api:$log4jVersion"
  testRuntimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
}

compileJava {
  options.incremental = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "11"
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
  }
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true
  }
  minHeapSize = "512m"
  maxHeapSize = "2048m"
}

java {
  withSourcesJar()
  withJavadocJar()
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  repositories {
    maven {
      url = isSnapshot ?
          "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
          "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
        password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.kotlin
      artifact sourcesJar
      artifact javadocJar
      artifactId = "fledutils"
      pom {
        name = project.name
        packaging = "jar"
        description = "A set of utilities common for the fledware code"
        url = "https://github.com/fledware/FledUtils/"
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = "scm:git:git@github.com:fledware/fledutils.git"
          developerConnection = "scm:git:git@github.com:fledware/fledutils.git"
          url = "https://github.com/fledware/FledUtils/"
        }
        developers {
          developer {
            id = 'rexfleischer'
            name = 'Rex Fleischer'
          }
        }
      }
    }
  }
}

signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
  required { gradle.taskGraph.hasTask("publish") }
  sign publishing.publications.maven
}
